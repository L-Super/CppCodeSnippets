; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "QtPacker"
#define MyAppPublisher "My Company, Inc."
#define MyAppURL "https://www.example.com/"
#define MyAppExeName "QtPacker.exe"

;[Registry]
#define MyAppAssocName MyAppName + " File"
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

#define MyCopyright "Copyright (C) 2022-2099 My Company, Inc."
#define AppPath "C:\Users\leo\Desktop\qpacker"
;从待打包程序获取版本
#define MyAppVersion GetVersionNumbersString(AddBackslash(AppPath) + MyAppExeName)
#define MyIcon "C:\Users\leo\Desktop\qpacker\yellowman.ico"
#define MyOutputDir "C:\Users\leo\Desktop\packer-project\Publish"
#define MyOutputFilename "mysetup"


[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{FC240650-B5D7-462A-B85B-012115F8B141}
AppName={#MyAppName}
;被安装应用程序的版本号。并被显示在应用程序添加/删除程序 的版本栏中
AppVersion={#MyAppVersion}
;指定应用程序的名称加上它的版本号,被显示在安装程序向导的欢迎 页面，并被作为应用程序添加/删除程序 的默认标题
AppVerName={#MyAppName} {#MyAppVersion}         
AppCopyright={#MyCopyright}
;被显示在“控制面板”程序的添加/删除程序 的“支持程序”对话框中
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
;用于选择目标位置 向导页面中的默认目录名称
;DefaultDirName={autopf}\My Program在安装程序中，通常会显示:C:\Program Files\My Program
DefaultDirName={autopf}\{#MyAppName}
;该指令设为 yes，安装程序将在安装结束时告诉资源管理器刷新它的文件关联信息，并且在卸载完成后执行同样操作。
;如果你的安装程序创建了文件关联，但没有将 ChangesAssociations 设为 yes，文件类型的正确图标很可能不能显示，直到用户注销或重新启动电脑。
ChangesAssociations=yes
DisableProgramGroupPage=yes
; 以下行取消注释，以在非管理安装模式下运行（仅为当前用户安装）。
;PrivilegesRequired=lowest
OutputDir={#MyOutputDir}
OutputBaseFilename={#MyOutputFilename}
;为安装程序/卸载程序指定一个新的图标
SetupIconFile={#MyIcon}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
;在安装程序已经运行时，该指令用来阻止安装程序再运行。
SetupMutex={#MyAppName},Global\{#MyAppName}
;防止用户在应用程序仍在运行的情况下安装新的应用程序版本，以及防止用户卸载正在运行的应用程序。
;需要在main()里添加 CreateMutex(NULL, FALSE, "MyProgramsMutexName");代码才有效
;AppMutex={#MyMutexName},Global\{#MyMutexName}
;添加许可文件
;LicenseFile=readme.txt

[Languages]
Name: "chinesesimplified"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#AppPath}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\D3Dcompiler_47.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\libEGL.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\libgcc_s_seh-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\libGLESv2.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\libstdc++-6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\libwinpthread-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\opengl32sw.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\Qt5Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\Qt5Gui.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\Qt5Svg.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\Qt5Widgets.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#AppPath}\iconengines\*"; DestDir: "{app}\iconengines"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#AppPath}\imageformats\*"; DestDir: "{app}\imageformats"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#AppPath}\platforms\*"; DestDir: "{app}\platforms"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#AppPath}\styles\*"; DestDir: "{app}\styles"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
var
//ErrorCode: Integer;
IsRunning: Integer;

// 安装时判断客户端是否正在运行 
function InitializeSetup(): Boolean;
begin 
  Result :=true; //安装程序继续 
  IsRunning:=FindWindowByWindowName('{#MyAppName}'); 
  while IsRunning<>0 do 
  begin 
    if Msgbox('安装程序检测到客户端正在运行。' #13#13 '您必须先关闭它再进行安装', mbInformation, MB_OK) = idOK then 
    begin 
      Result :=false; //安装程序退出 
      IsRunning :=0; 
    end;
  end; 
end; 

// 卸载时判断客户端是否正在运行 
function InitializeUninstall(): Boolean; 
begin 
  Result :=true; //安装程序继续 
  IsRunning:=FindWindowByWindowName('{#MyAppName}'); 
  while IsRunning<>0 do 
  begin 
    if Msgbox('安装程序检测到客户端正在运行。' #13#13 '您必须先关闭它，然后单击“是”继续卸载，或“否”退出！', mbConfirmation, MB_YESNO) = idNO then 
    begin 
      Result :=false; //安装程序退出 
      IsRunning :=0; 
    end else begin 
      Result :=true; //安装程序继续 
      IsRunning:=FindWindowByWindowName('{#MyAppName}'); 
    end; 
  end; 
end; 

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep); 
begin 
  case CurUninstallStep of 
  usUninstall: 
  begin // 开始卸载 
    end; 
    usPostUninstall: 
    begin // 卸载完成 
      //MsgBox('CurUninstallStepChanged:' #13#13 'Uninstall just finished.', mbInformation, MB_OK); 
      //插入代码执行卸载后的任务
      //ShellExec('open', 'http://www.baidu.com', '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode); 
    end; 
  end; 
end; 